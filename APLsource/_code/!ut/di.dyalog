 di←{⍺←⊢ ⋄ p←⍺⊣0
     ⎕PP←{0::⍵ ⋄ ∇ ⎕PP←1+⍵}⎕PP
     (⍴⍵){
         1<|≡⍵:⍺ ∇{⍵/⍨' '∨.≠⍵}⎕FMT(⍴¨⍵)∇¨⍵⍴⍨1⌈⍴⍵
         (⊃'~''#'/⍨(⍬''∊⊂0⍴⍵),1){
             ⍉↑⍺{(⊂⌽⍺,1↓⌽⊃⍵),1↓⍵}↓⍉⍵
         }(⎕D,⎕A,'abcdefghijklmnopqrstuvwxyz','*')[62⌊↑,↓⍺]{ ⍝ use '*' for larger than 61
             (⍴⍵)⍴(' '≠⍺↑⍨⍴⍵)⊖↑⍵ ⍺}⍺{⍵/⍨1⌈1+(0⊥⍴⍵)↑-2-⍴⍺
         }p bx ⎕FMT ⍵⍴⍨1⌈⍴⍵
     }⍵
⍝ display
⍝ ⍺ internal
⍝ ⍵ any array
⍝ ← display form
⍝ Phil Last ⍝ 2008-01-23 13:35
⍝ {{⊂[¯2↑⍳⍴⍴⍵]⍵}⍣(⌊0.5×⍬⍴⍴⍴⍵)⊢⍵}
⍝ Phil Last 2017-07-01 09.06
 }
