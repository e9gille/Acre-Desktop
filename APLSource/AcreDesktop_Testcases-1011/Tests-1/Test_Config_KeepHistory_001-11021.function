 R←Test_Config_KeepHistory_001(stopFlag batchFlag);⎕TRAP;projectPath;ref;projectName;buff;result;list;configFilename
⍝ Check whether `KeepHistory←'Off'` is honored but `]acre.SetChanged` keeps working.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'\',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''

 result←∆RunUcmd'Createproject ',projectPath,' #.',projectName

 ref.⎕FX'r←Hello' 'r←''World2'''
 result←∆RunUcmd'SetChanged #.',projectName,'.Hello'
 →GoToTidyUp 0∊⍴result
 →GoToTidyUp 1≠⍴F.ListFiles projectPath,'\.acre\changes\'
 →GoToTidyUp 1≠⍴list←F.ListFiles projectPath,'\.acre\changes\'
 F.DeleteFile list

 F.RmDir projectPath
 result←∆RunUcmd'Createproject ',projectPath,' #.',projectName
 configFilename←projectPath,'\acreconfig.txt'
 buff←A.ReadUtf8File configFilename
 buff←'KeepHistory←''On'''⎕R'KeepHistory←''Off'''⊣buff
 A.WriteUtf8File configFilename buff
 result←∆RunUcmd'OpenProject ',projectPath,' #.',projectName
 →GoToTidyUp 0≠F.IsDir projectPath,'\.acre'
 ref.⎕FX'r←Hello' 'r←''World2'''
 result←∆RunUcmd'SetChanged #.',projectName,'.Hello'
 →GoToTidyUp 0∊⍴result

 R←∆OK

∆TidyUp:
 F.RmDir projectPath
