 R←Test_Config_Open_010(stopFlag batchFlag);⎕TRAP;projectPath;ref;projectName;buff;list;projectName2;projectPath2;ref2;noOfProjects
⍝ Create a project and check whether `Open` with `-track=off` is honored.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 noOfProjects←⊃⍴∆RunUcmd'Projects'
 projectName←⊃⎕SI
 projectPath←F.EnforceSlash ∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 #.⎕SHADOW'AcreMessages'
 #.AcreMessages←''

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject ',projectPath,' #.',projectName
 :Else
     →GoToTidyUp 1
 :EndTrap

 projectName2←(⊃⎕SI),'_2'
 projectPath2←F.EnforceSlash ∆PATH,'/',projectName2
 #.⎕SHADOW projectName2
 ref2←⍎projectName2 #.⎕NS''
 ref2.⎕FX'r←Hello' 'r←''Universe'''

 :Trap (~stopFlag)/0
     {}∆RunUcmd'Createproject ',projectPath2,' #.',projectName2
 :Else
     →GoToTidyUp 1
 :EndTrap

 #.⎕EX¨projectName projectName2
 #.⎕SHADOW'Temp'
 buff←''
 buff,←⊂':Namespace'
 buff,←⊂'   Open←''',projectPath2,' #.Temp -track=off'''
 buff,←⊂'   ProjectSpace←''#.',projectName,''''
 buff,←⊂':EndNamespace'
 A.WriteUtf8File(projectPath,'/acreconfig.txt')buff

 :Trap (~stopFlag)/0
     list←∆RunUcmd'OpenProject ',projectPath,' -messages=''#.AcreMessages'''
 :Else
     →GoToTidyUp 1
 :EndTrap

 →GoToTidyUp 9=#.⎕NC projectName2
 →GoToTidyUp 0=#.⎕NC'Temp'
 →GoToTidyUp 3≠#.Temp.⎕NC'Hello'
 →GoToTidyUp'Universe'≢#.Temp.Hello
 →GoToTidyUp 6≠⍴#.AcreMessages
 →GoToTidyUp 1≠(⊃⍴∆RunUcmd'Projects')-noOfProjects

 R←∆OK

∆TidyUp:
 :Trap 0 ⋄ F.RmDir projectPath ⋄ :EndTrap
 :Trap 0 ⋄ F.RmDir projectPath2 ⋄ :EndTrap
