 R←Test_Misc_007(stopFlag batchFlag);⎕TRAP;projectName;projectPath;ref;result
⍝ Enforce a name clash
⍝ acre should prevent the user from doing stupid stuff like this.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FX'r←Hello' 'r←''World'''
 #.⎕SHADOW'TEMP'
 'TEMP'#.⎕NS''
 #.TEMP.Log←''

 :Trap (~stopFlag)/0
     result←∆RunUcmd'Createproject ',projectPath,' #.',projectName
 :Else
     →GoToTidyUp 1
 :EndTrap

 ref.⎕EX'Hello'                                 ⍝ Erase from WS but do not tell acre!
 'Hello'ref.⎕NS''                               ⍝ Create a namespace with the name of that function - that's a name clash.
 ref.Hello.⎕FX'r←Foo' 'r←''Universe'''          ⍝ acre does not care about empty namespaces, so we need to populate it with something, anything.
 result←∆RunUcmd'SetChanged #.',projectName,'.Hello.Foo -messages=#.TEMP.Log'
 →GoToTidyUp 0∊⍴result
 →GoToTidyUp~0∊⍴#.TEMP.Log
 :Trap 11 911
     result←∆RunUcmd'OpenProject ',projectPath,' -messages=#.TEMP.Log'
     →GoToTidyUp 1
 :EndTrap
 →GoToTidyUp 1≠⍴result
 →GoToTidyUp 1≠∨/' Not fixed '⍷∊#.TEMP.Log
 →GoToTidyUp 1≠⍴'File: .* may require attention.'⎕S 0⍠('Mode' 'D')⊣#.TEMP.Log

 R←∆OK

∆TidyUp:
 F.RmDir projectPath
⍝Done
