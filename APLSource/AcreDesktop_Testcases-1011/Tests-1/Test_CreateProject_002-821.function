 R←Test_CreateProject_002(stopFlag batchFlag);⎕TRAP;path;rc;en;more;projectPath;ref;projectName;result;list;errMsg
⍝ Tries to create a project on an invalid path.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 :If 'Win'≢A.GetOperatingSystem ⍬
     R←∆WindowsOnly
 :Else
     R←∆Failed

     projectName←⊃⎕SI
     projectPath←'QWE:/This_does_almost_certainly_Not_Exist/aa/bb/cc/dd'
     #.⎕SHADOW projectName
     ref←⍎projectName #.⎕NS''
     ref.⎕FX'r←Hello' 'r←''World'''

     :Trap 0
         result←⎕SE.UCMD'acre.Createproject ',projectPath,' #.',projectName
         →FailsIf 1
     :EndTrap

     errMsg←1⊃⎕DM
     →PassesIf'FILE NAME ERROR'≡(⍴'* Command Execution Failed: ')↓errMsg~⎕UCS 8
     R←∆OK

 :EndIf
