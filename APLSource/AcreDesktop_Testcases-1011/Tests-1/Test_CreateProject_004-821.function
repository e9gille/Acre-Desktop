 R←Test_CreateProject_004(stopFlag batchFlag);⎕TRAP;projectPath;ref;projectName;result;list;noOf
⍝ Create a project with a nested namespace. Top level has namespace script, sub level a ref pointing to this.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 R←∆Failed

 projectName←⊃⎕SI
 projectPath←∆PATH,'/',projectName
 #.⎕SHADOW projectName
 ref←⍎projectName #.⎕NS''
 ref.⎕FIX':Namespace Foo' '∇r←Hello' 'r←''World''' '∇' ':EndNamespace'
 'Sub'ref.⎕NS''
 ref.Sub.Goo←⍎'#.',projectName,'.Foo'
 noOf←⊃⍴⎕SE.UCMD'acre.Projects'

 :Trap (~stopFlag)/0
     result←⎕SE.UCMD'acre.Createproject ',projectPath,' #.',projectName,' [ messages←''off'' ]'
 :Else
     →GoToTidyUp 1
 :EndTrap

 →GoToTidyUp 0=F.IsDir projectPath,'/APLSource'
 →GoToTidyUp 0=F.IsDir projectPath,'/.acre'
 →GoToTidyUp 0=F.IsFile projectPath,'/acreconfig.txt'
 →GoToTidyUp(F.EnforceSlash projectPath,'/APLSource/Foo-1.script')≢F.EnforceSlash⊃⊃F.Dir projectPath,'/APLSource/*'

 R←∆OK

∆TidyUp:
 F.RmDir projectPath
