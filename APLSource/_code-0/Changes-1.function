 Changes←{dat←⍺
     ~dat.hist:⍪''⊣dat noHistMsg''
     null⊢all←(⊢(/⍨)0<⎕NC)dat changes'':⍪''
     dat.nspath≡⊃⍵:{         ⍝ project changes
         date←(dat sourceReg'').(date name)UT.co(1+≢dat.nspath)↓¨⍵
         name date←(⊂⊂⍒date)⌷¨⍵ date
         dt←OPT.when
         dt≡0:⍪name
         res←name,⍪↓DT.dt2fm date
         dt≡1:res
         lt←1∊'<≤'∊dt
         res⌿⍨lt≠date≥DT.fm2dt dt~'<≤≥>'
     }all

     null⊢item←⍬⍴⍵∩all:⍪''   ⍝ item change
     file←⊃dat getChangeFilenames item
     cmp←⌽1↓⍳1⊃⎕FSIZE⊢tie←file ⎕FTIE 0
     ptr val←UT.ht ⎕FREAD tie,⊂cmp
     cmp←1↓cmp
     date←↓MS.DK,⍤1 DT.(dt2fm∘ts2dt∘fd2ts)⊢/∘⎕FRDCI¨tie,¨cmp
     z←⎕FUNTIE tie
     ⍪⍪¨' ',¨(⊂¨↓⍕((cmp=ptr)\⊂'>>>'),(⊂MS.BF),cmp,⍪date),¨val
     ⍪⊃,/,(⊂⊂''),(⊂¨↓⍕((cmp=ptr)\⊂'>>>'),(⊂MS.BF),cmp,⍪date),⍪val
⍝ list changed items of project
⍝ or dates and values of item
⍝ Phil 2018-06-19 09.01
 }
