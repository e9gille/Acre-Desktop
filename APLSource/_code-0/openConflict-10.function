 openConflict←{dat←⍺
     0=⍴old←openProjects'':⍬
     new←,dat
     paths←old.(nspath,'.')
     path←new.(nspath,'.')
     path∊paths:''                    ⍝ same space - ok
     ∨/⊃¨path⍷¨paths:''               ⍝ over - ok
     b←⊃¨paths⍷¨path                  ⍝ new in old - forbidden
     ∨/b:MS.CA showList(new,b/old).nspath
     ⍬

⍝ ⍺ dat
⍝ ← null or msgs
⍝   The order in this function is paramount.
⍝   implements rule:
⍝       A project may not be opened inside another
⍝ if same, space is expunged and closed before open
⍝ if over, spaces are expunged and closed before open
⍝ if under, would be in conflict with parent
⍝ Phil Last 2017-12-09 18.57
 }
