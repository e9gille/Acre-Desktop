 userCmdRun←{
     (method string parser space path wsid name)←⍵
     z←Start path wsid name                  ⍝ once
     SV.space←space,'.'                      ⍝ whence called
     z←openProjects''                        ⍝ still open?
     str com←(∧\quoted≥'⍝'=⊢)UT.sp string    ⍝ sep comments
     str opt←(∧\quoted≥'['=⊢)UT.sp str       ⍝ sep opts
     strs←UT.sv str                          ⍝ words and strings
     opt←{0::⎕NS'' ⋄ AN.decode ⍵,'←]'~⍵}opt
     z←{⍎'OPT.',⍵,'←opt.',⍵}/0,⍪⊃∩/(opt OPT).⎕NL-2 ⍝ see optionFields
     (item proj share)←'i' 'p' 's'=⊃parser   ⍝ see allMethods
     res←'res'{⍵,⍺/⍨null ⍵}OPT.result        ⍝ local if not assigned
⍝                    ↓↓↓↓↓
     res←res{⍎⍺,'←⍵'}method{
         item:(⍎⍺)itemMethod ⍵               ⍝ items of arg are names
         proj:(⍎⍺)projMethod ⍵               ⍝ first item is project
         share:(⍎⍺)sharedMethod ⍵            ⍝ arg is specific to method
     }strs
     OPT.rows(⊢↑⍨⊣⌊⊢∘≢)res
⍝ ⍵ method; arg string; type of fn; whence called; whence loaded; system.
⍝ Phil Last 2018-01-15 22.16
 }
