 userCmdRun←{
     (method string parser space path wsid name)←⍵
     z←Start path wsid name                  ⍝ once
     SV.space←space,'.'                      ⍝ whence called
     z←openProjects''                        ⍝ still open?
     str com←((0⍳⍨(∧\quoted≥'⍝'=⊢))(↑_↓)⊢)string
     str opt←((0⍳⍨(∧\quoted≥'['=⊢))(↑_↓)⊢)str
     strs←UT.sv str                          ⍝ words and strings
     opt←{⎕NS''}if{9≠⎕NC'f'⊣f←⍵}{0::''⊣showMsg UT.bx MS.DN,MS.DO,⍵
         AN.decode ⍵}if('←'∘∊)opt
     z←{⍎'OPT.',⍵,'←opt.',⍵}/0,⍪⊃∩/(opt OPT).⎕NL-2 ⍝ see optionFields
     (item proj share)←'i' 'p' 's'=⊃parser   ⍝ see all Methods
     res←'res'{⍵,⍺/⍨null ⍵}OPT.result        ⍝ local if not assigned
     res←res'edit'⊃⍨ed←≡/CC res'⎕ED'
⍝                    ↓↓↓↓↓
     res←res{⍎⍺,'←⍵'}method{                 ⍝ result now in res and 'res'
         item:⍺(⍎⍺)itemMethod ⍵              ⍝ items of arg are names
         proj:⍺(⍎⍺)projMethod ⍵              ⍝ first item is project
         share:⍺(⍎⍺)sharedMethod ⍵           ⍝ arg is specific to method
     }strs
     z←UT.ed∘ntbs∘⎕FMT if ed⊢res
     0=≢r←OPT.rows:res
     ⍪(⊢↑⍨r⌊≢)ntbs ⎕FMT res
⍝ ⍵ method; arg string; type of fn; whence called; whence loaded; system.
⍝ Phil 2018-01-28 00.43
 }
