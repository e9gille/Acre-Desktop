 userCmdRun←{
     (new method arg type space path wsid name)←⍵
     z←Start⍣new⊢path wsid name              ⍝ once
     SV.debug←1∊⎕SE.SALTUtils.DEBUG          ⍝ OK?
     SV.space←space,'.'                      ⍝ whence called
     z←openProjects''                        ⍝ still open?
     ⎕THIS.OPT←⎕NS arg.(⎕NS'qwertyuiopasdfghjklzxcvbnm'⎕NL 2.1)
     (item proj share)←'i' 'p' 's'=⊃type     ⍝ see allMethods
⍝         ↓↓↓↓↓
     res←method{
         item:⍺(⍎⍺)itemMethod ⍵              ⍝ items of arg are names
         proj:⍺(⍎⍺)projMethod ⍵              ⍝ first item is project
         share:⍺(⍎⍺)sharedMethod ⍵           ⍝ arg is specific to method
     }arg.Arguments
     res
⍝ ⍵ method; arg string; type of fn; whence called; whence loaded; system.
⍝ Phil 2018-01-28 00.43
 }
