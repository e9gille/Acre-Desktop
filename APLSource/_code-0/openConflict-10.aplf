 openConflict←{dat←⍺
     0=⍴old←openProjects'':⍬
     new←,dat
     paths←old.(nspath,'.')
     path←new.(nspath,'.')
     path∊paths:''                    ⍝ same space - ok
     ∨/⊃¨path⍷¨paths:''               ⍝ over - ok
     b←⊃¨paths⍷¨path                  ⍝ new in old - forbidden
     ∨/b:MS.DE showList(new,b/old).nspath
     ⍬

⍝ ⍺ dat
⍝ ← null or msgs
⍝   The order in this function is paramount.
⍝   implements rule:
⍝       A project may not be tracked inside another
⍝ if same, space is expunged and closed before loading
⍝ if over, spaces are expunged and closed before loading
⍝ if under, would be in conflict with parent
⍝ Phil 2018-06-06 21.11
 }
