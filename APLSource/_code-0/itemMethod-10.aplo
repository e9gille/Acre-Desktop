 itemMethod←{⍺←⊢ ⋄ mon←1≡⍺ 1
     mon<null⊢items←⍵:''⊣logMsg MS.DM,SX.⍎⍺
     0∊⍴ids←openProjects'':''⊣logMsg MS.GX
     items←prefSpace items   ⍝ prefix non-FQNs
     no←0=dat←whichProject items
⍝     z←logMsg MS.DI showList no/items
     z←MS.DI logMsg no/items
     items dat/⍨←⊂~no
     0∊⍴items:''⊣logMsg MS.DH
⍝       ↓↓↓↓
     ⊃if≢⍺⍺/dat{⍺ ⍵}⌸items

⍝ [⍺]  name of method
⍝ ⍺⍺   method to run - takes instance data space left arg
⍝ ⍵    args
⍝ args random list of items of possiby disparate projects
⍝ Run instance method ⍺⍺ for each subgroup of ⍵
⍝   corresponding to a tracked project
⍝   Methods expected to take 1 or more names and return a subset
⍝ ← results per project, '' if none
⍝ Phil 2018-06-06 21.11
 }
