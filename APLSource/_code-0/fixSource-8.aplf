 fixSource←{dat←⍺ ⋄ ref←⍵≡MS.BG ⋄ prev←dat getTime''
     src tgt←dat.(source nspath),¨'' '.'           ⍝ ../APLSource/
     (path date)←{⍵.(Path Date)}fileTree⊢src       ⍝ all files
     path date/⍨←⊂~∨/'/.'⍷(≢src)↓⍤1↑path           ⍝ ignore all /.*/
     file type←↓⍉↑(UT.px(≢src)↓⊢)¨path             ⍝ 'ns/path/file' '.type'
     path date file type/⍨←∊/Cc type(MS.AZ,MS.AY)  ⍝ only valid types
     name←'/.'decodeCaps file
     path date file name type/⍨←⊂ref≤new←date>prev ⍝ new or not refresh
     vars←'.array' '.charlist' '.charmat' '.charstring'
     path date file name type/⍨←⊂type∊vars
⍝     dup←(⊢∊⊢(/⍨)~∘nub)name
⍝     z←logMsg MS.DD showList,/(tgt∘,¨dup/name),'=',⍪dup/type
     z←logMsg MS.BC,MS.EO,((≢path)UT.pl MS.FL),MS.EY,¯4 tPath src
     read←{⊃MS.HI ⎕NGET ⍵ 1}
     val←read¨UT.tk path
     chg val←↓⍉↑{
         c v←↓⍉↑{(∨/b),⊂⍵/⍨1+b←b>(1↓b,0)∨¯1↓0,b←'\'=⍵}¨⍵
         (∨/c)v
     }¨val
     ~∨/chg:'none'MS.BV
     val path name/⍨←⊂chg
     write←{⍺ MS.HI ⎕NPUT ⍵ 1}
⍝     z←logMsg(MS.EN,(≢name)UT.pl MS.FN)showList name
     z←(MS.EN,(≢name)UT.pl MS.FN)showmsg name
     'done'⊣val write¨UT.tk path

⍝ ⍺ dat
⍝ ⍵ ? or MS.BG ('Refresh')
⍝ ← ⍵ or fqns of refreshed items
⍝ get source and fix; if refresh fetch only changes
⍝ Phil 2018-07-18 19.47
 }
