 getChanges←{dat←⍺ ⋄ empty←6⍴⊂''
     tgt←dat.nspath,'.'
     path←{⍵.Path}folderObj dat.changes
     null path:empty                ⍝ '/changes/path.name.type.change'
     res ext←↓⍉↑nx¨path          ⍝          'path.name.type' '.change'
     path res ext chg/⍨←⊂2>chg←⊃⍳/cc(MS.EX MS.HN)ext  ⍝ del chg
     null path:empty
     res type←↓⍉↑nx¨res          ⍝ 'path.name.type' → 'path.name' '.type'
     path res type chg/⍨←∊/cc type MS.NC
     fqn←tgt∘,¨'..'decodeCaps res
     dat.changed←fqn
     dn dp dt←fqn path type/¨⍨⊂~chg  ⍝ dels
     fqn path type/⍨←⊂chg            ⍝ chgs
     val←currentChange/0,⍪path
     dv←currentChange/0,⍪dp
     fqn val type dn dv dt
⍝ ⍺ dat
⍝ ⍵ ?
⍝ ← names, values and type of changed items and of deleted
⍝   changefiles: .change and .delete
⍝   fix values elsewhere
⍝ Phil Last 2018-01-14 18.55
 }
