 Changes←{dat←⍺
     ~dat.hist:⍪''⊣dat noHistMsg''
     null⊢name date←⊃{⍺ ⍵/¨⍨⊂0<⎕NC ⍺}/dat changes'':⍪''
     when←{⍵≡1≡⍵:⍵ ⋄ DT.fm2dt ⍵}OPT.when
     dat.nspath≡⊃⍵:{         ⍝ project changes
         name date←(⊂⊂⍒date)⌷¨name date
         when=0:⍪name
         res←name,⍪↓DT.dt2fm date
         when=1:res
         res⌿⍨date≥when
     }0

     null⊢name∩item←⍬⍴⍵:⍪''⊣logMsg MS.BV excRep⊃item   ⍝ item change
     file←⊃dat getChangeFilenames item
     ptr cmp←UT.ht⌽1↓⍳1⊃⎕FSIZE⊢tie←file ⎕FTIE 0
     date←DT.(ts2dt∘fd2ts)⊢/∘⎕FRDCI¨tie,¨cmp
     head←↓MS.DK,⍤1 DT.dt2fm date
     ptr val←UT.ht ⎕FREAD tie,⊂ptr,cmp/⍨when≠1
     head←↓⍕((cmp=ptr)\⊂'>>>'),(⊂MS.BF),cmp,⍪head
     z←⎕FUNTIE tie
     when=1:⍪head
     res←⍪⍪¨' ',¨(⊂¨head),¨val
     when=0:res
     res⌿⍨date≥when
⍝ list changed items of project
⍝ or dates and values of item
⍝ Phil 2018-08-24 20.38
 }
