 fixValue←{null ⍵:'' '' ''
     fixNs←(⍎''⎕NS⍨⊢)eu
     fixSrc←{0::0 ⋄ 1⊣⍺.⎕FIX ⍵}
     fixVar←{0::0 ⋄ n s v←⍵ ⋄ 1⊣n s.{⍎⍺,'←⍵'}⍺⍺ v}
     fixFn←{0::0 ⋄ n s v←⍵ ⋄ n≡s.⎕FX v}
     fixClss←~{⍵\~1↓0,fixSrc⌿⍵/⍺}⍣≡

     fqn typ val←,∘⊂¨if(≡⊃)⍵
     z←(1↓¨nms)(ns←⎕NS'').{⍎⍕⍺,'←⍵'}⍳⍴nms←MS.AZ
     nc←nms⍳typ
     nam←1↓¨1⊃spc nam←↓⍉↑namx¨fqn
     res←0⍴⍨⍴nam
     (x/spc)←fixNs spc/⍨x←nc=ns.script  ⍝ parents of scripts
     (x/res)←fixSrc⌿↑spc val/¨⍨⊂x←nc=ns.script
     (x/spc)←fixNs spc/⍨x←nc≠ns.script  ⍝ complete space-tree
     (x/res)←AN.decode fixVar/0,⍪↓⍉↑nam spc val/¨⍨⊂x←nc=ns.array
     (x/res)←⊢fixVar/0,⍪↓⍉↑nam spc val/¨⍨⊂x←nc=ns.charlist
     (x/res)←↑fixVar/0,⍪↓⍉↑nam spc val/¨⍨⊂x←nc=ns.charmat
     (x/res)←{1↓⊃,/UT.CR,¨⍵}fixVar/0,⍪↓⍉↑nam spc val/¨⍨⊂x←nc=ns.charstring
     (x/res)←fixFn/0,⍪↓⍉↑nam spc val/¨⍨⊂x←nc∊ns.(function operator)
     (x/res)←(↑x∘/¨spc val)∘fixClss 1/⍨+/x←nc∊ns.(class interface)

     res←res>MS.AL''∊⍨now←nameClass fqn
     z←showMsg MS.FX showList fqn/⍨~res  ⍝ not fixed
     fqn typ now val/⍨←⊂res
     chg←typ≢¨now
     z←showMsg MS.AD showList chg/,/fqn,' ',typ,'>',⍪now ⍝ type change
     fqn now val
⍝ ⍵ fqn type value
⍝ ← ⍵ fixed
⍝ Phil 2018-02-02 18.48
 }
