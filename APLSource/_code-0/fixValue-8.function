 fixValue←{null ⍵:'' '' '' ''
     ⍺←⊢ ⋄ ren←⍺⊣0
     fqn typ val←,∘⊂¨if(≡⊃)⍵
     fqn typ val{⍺[⍵]}←⊂⍋(≢sPath)¨fqn
     ns←(1↓¨MS.AY)dix 1↓¨MS.AZ
     nc←⊃⌊/MS.AY MS.AZ⍳¨⊂typ
     nam←1↓¨1⊃spc nam←↓⍉↑namx¨fqn
     ers←⍳0 2⊣res←0⍴⍨⍴nam
     fixNs←⍎''⎕NS⍨⍕
     fixSrc←{0::0 ⋄ 1⊣(fixNs ⍺).⎕FIX ⍵}
     (x/res)←fixSrc⌿↑spc val/¨⍨⊂x←nc=ns.script
     spc←fixNs eu spc
     data←↓⍉↑(⍳⍴nam)nam spc val
     fixVar←{0::0 ⋄ x n s v←⍵ ⋄ 1⊣n s.{⍎⍺,'←⍵'}⍺⍺ v}
     fixFn←{0::0 ⋄ x n s v←⍵ ⋄ n≡r←s.⎕FX v:1 ⋄ 0∊∊r:0 ⋄ 1⊣ers⍪←x r}
     fixSrc←{0::0 ⋄ x n s v←⍵ ⋄ n≡r←'.'UT.nn⍕s.⎕FIX v:1 ⋄ 1⊣ers⍪←x r}
     fixClss←~{⍵\~1↓0,fixSrc/0,⍪⍵/⍺}⍣≡

     (x/res)←AN.decode fixVar/0,⍪data/⍨x←nc=ns.array
     (x/res)←⊢fixVar/0,⍪data/⍨x←nc=ns.charlist
     (x/res)←↑fixVar/0,⍪data/⍨x←nc=ns.charmat
     (x/res)←{1↓⊃,/UT.CR,¨⍵}fixVar/0,⍪data/⍨x←nc=ns.charstring
     (x/res)←fixFn/0,⍪data/⍨x←nc∊ns.(function operator)
     (x/res)←(x/data)∘fixClss 1/⍨x←nc∊ns.(class interface)
     ers←{p←⊃'.'UT.pn⊃f t←⍺⊃¨fqn typ ⋄ (⍺⊃typ)←c←⊃nameClass⊢(⍺⊃fqn)←n←p,⍵
         f n⊣showMsg'* ',t,' ',f,MS.DO,c,' ',n
     }/ers⍪{⍵,⍪'.'UT.nn¨fqn[⍵]}~∘(⊣/ers){⍵/⍳⍴⍵}res∧typ≢¨nameClass fqn
     z←showMsg,/(⊂MS.DJ),⍉↑typ' 'fqn/¨⍨⊂~res
     (res∘/¨fqn typ val),ren/⊂ers
⍝ ⍵ fqn type value
⍝ ← ⍵ fixed - reordered, corrected and filtered
⍝ Phil 2018-07-12 17.44
 }
