 Install←{
     path wsid←2⍴,\1 ⎕NPARTS(⍵≡∊⍵)⊃⍵ ⎕WSID
     name←1↓{⍵/⍨1=+\⍵='.'}⊃⎕XSI
     apo←'"'∘(qu←UT.qu)
     sep←⊃~∘'∘'⊢⎕SE.SALT.PATHDEL                           ⍝ thanks Gil
     old←1↓¨{⍵⊂⍨⍵∊sep,'∘'}sep,⎕SE.SALT.Settings'cmddir'    ⍝ seek both
     f←{⍵/⍨(⊢=⌈/)(+\×~)⍵∊'/\'}                             ⍝ last /substr/
     old←⊃,/sep,¨old/⍨~¯1(↓∊↑)CC f¨old,⊂path               ⍝ sans me
     z←⎕SE.SALT.Settings'cmddir "',path,old,'" -permanent' ⍝ add me
     cmds grp parse desc help←⍕¨↓⍉qu¨allMethods name
     re←{⊃⍵{(⍴,⍵)↓⊃,/⍵∘,¨(⍴,⍺)↓¨⍺{⍵⊂⍨⍺⍷⍵}⍺,⍺⍺}/⍺}
     data←(name)(grp)(cmds)(desc)(parse)(help)
     code←'<n>' '<g>' '<c>' '<d>' '<a>' '<h>'
     script←,(⎕UCS 10),{0 3↓⍵⌿⍨>/'⍝: '⍷⍵}⎕CR⊃⎕SI
     script←⊂(⎕UCS 10){1↓¨⍵⊂⍨⍵=⍺}⊃re/(↓code,⍪data),⊂script
     z←script ⎕NPUT(path,name,'.dyalog')1    ⍝ 1=rewrite
     rel←path,'releaseNotes','.txt'
     z←(⊂{⍵/⍨∧\(⍵∊∊⍵)∨⍵∨.≠¨'-'}releaseNotes⊣'')⎕NPUT rel 1
     ⍪'Installed at ' ']open ',∘apo¨path rel
⍝:  :namespace <n>Init
⍝:     ⎕io ⎕ml←0
⍝:     name  ← ,'<n>'  ⍝ string
⍝:     group ← ,¨<g>   ⍝  list
⍝:     cmds  ← ,¨<c>   ⍝  list
⍝:     desc  ← ,¨<d>   ⍝  list
⍝:     parse ← ,¨<a>   ⍝  list
⍝:     help  ← ,¨<h>   ⍝  list
⍝:   ∇ r←List
⍝:     r←{⎕NS''}¨⍳⍴cmds
⍝:     r.(Name Desc Group)←↓⍉↑cmds desc group
⍝:     r.(Parse)←⊂''
⍝:   ∇
⍝:    Help←{⍝ ↑(2 2⊤1+3|⍺)/desc help⊃¨⍨cmds⍳⊂⍵
⍝:          ⍕⍪'' 'Syntax: ',¨desc help⊃¨⍨cmds⍳⊂⍵
⍝:    }
⍝:    Run←{cmd arg←⍵
⍝:     (path wsid)←,\2⍴1 ⎕NPARTS ##.SourceFile
⍝:     new←{⎕se.<n>._code. newSession''}⍣(~new)⊢new←0∊⎕SE.⎕NC'<n>'
⍝:     z←⎕SE.{6::0 ⋄ ⊢'#.<n>'∘⎕CY ⍵}⍣new⊢wsid
⍝:     nsi←((⎕XSI+.=¨'.')⍳1)⊃⎕NSI  ⍝ whence called
⍝:     parse←parse⊃⍨cmds⍳⊂cmd
⍝:   ⍝                    ↓↓↓↓
⍝:     ⎕SE.<n>._code. userCmdRun new cmd arg parse nsi path wsid name
⍝:    }
⍝:  :endnamespace
 }
