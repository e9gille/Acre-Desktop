 putSource←{dat←⍺
     null⊢fqn type value←,∘⊂¨if(≡⊃)⍵:⍵
     src tgt←dat.(nspath source)
     path←dat getSourceFilenames fqn
     (b/path)←{⍺ UT.re MS.AX ⍵}⌿↑path type/¨⍨⊂b←(1∊MS.AX⍷⊢)¨path
     tree←⎕MKDIR/3,⍪∪⊃∘⎕NPARTS¨path         ⍝ ensure folders
     z←(,∘⊂if≡¨value){(,⍺)MS.HI ⎕NPUT ⍵ 1}¨path
     ~dat.hist:fqn type value

     name←(1+≢src)↓¨fqn
     file←(≢tgt)↓¨(-≢¨type)↓¨path
     tree←fileTree tgt                      ⍝ get all Path Size Date
     date←tree.(Date Path)UT.co path        ⍝ Date for only files written
     reg←dat sourceReg''
     reg.(name file date)/⍨←⊂~reg.name∊name ⍝ update only files written
     reg.(name file date),⍨←name file date
     z←reg UT.aw dat.sourcereg
     fqn type value

⍝ ⍺ dat
⍝ ⍵ FQNs type values
⍝ ← ⍵ same - having written to sourcetree
⍝   write to source tree files
⍝   write to reg if dat.hist
⍝ Phil 2018-02-12 22.05
 }
